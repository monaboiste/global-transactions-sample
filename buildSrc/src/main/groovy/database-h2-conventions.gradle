plugins {
    id("org.liquibase.gradle")
}

dependencies {
    runtimeOnly(libs.h2)
    runtimeOnly(libs.p6spy)

    // Required dependencies when the liquibase runs a task
    liquibaseRuntime(libs.liquibase.core)
    liquibaseRuntime(libs.picocli)
    liquibaseRuntime(libs.h2)
}

/*
 * Scripts aren't located in `resources` so we don't need manually exclude them to optimize the jar size.
 * We opted for `src/db/liquibase` location.
 */
liquibase {
    jvmArgs "-Duser.dir=${projectDir}"
    activities {
        main {
            classpath "src/db"
            changelogFile "src/db/liquibase/changelog.yml"
            url "jdbc:h2:file:${rootDir}/data/tx;AUTO_SERVER=TRUE"
            username "sa"
            password "sa"
            logLevel "info"
        }
    }
    runList = "main"
}

tasks.register("database-migrate") {
    group("verification")
    description("Run database migration scripts")

    dependsOn(tasks.update)
}
